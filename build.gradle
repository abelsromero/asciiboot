import org.gradle.nativeplatform.platform.internal.DefaultNativePlatform
import org.springframework.boot.gradle.plugin.SpringBootPlugin

plugins {
  id 'org.springframework.boot' version '3.2.3'
  id 'java'
}

group = 'org.asciidoctor.it'
version = '0.0.1-SNAPSHOT'

repositories {
  mavenCentral()
}

ext {
  asciidoctorjVersion = '2.5.11'
  asciidoctorjPdfVersion = '2.3.13'
}

dependencies {
  implementation platform(SpringBootPlugin.BOM_COORDINATES)
  implementation "org.asciidoctor:asciidoctorj:${asciidoctorjVersion}"
  implementation "org.asciidoctor:asciidoctorj-pdf:${asciidoctorjPdfVersion}"

  implementation 'org.springframework.boot:spring-boot-starter-web'
  implementation 'org.springframework.boot:spring-boot-starter-actuator'

  testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

java {
  toolchain {
    languageVersion.set(JavaLanguageVersion.of(getToolchainVersion()))
  }
}

def getToolchainVersion() {
  if (DefaultNativePlatform.getCurrentOperatingSystem().isWindows()) {
    if (JavaVersion.current().isCompatibleWith(JavaVersion.VERSION_19))
      return 19
  }
  return 17
}

bootJar {
  requiresUnpack '**/asciidoctorj-*.jar'
}

test {
  useJUnitPlatform {
    excludeTags "e2e"
  }
}

tasks.register("e2eTest", Test) {
  dependsOn project.tasks.named('bootJar')
  useJUnitPlatform {
    includeTags "e2e"
  }
}
